if(WIN32)
    set(SDK_TOBII_EYEX "" CACHE PATH "")
    if(SDK_TOBII_EYEX)
        otr_module(tracker-tobii)
        set(tobii-libdir ${SDK_TOBII_EYEX}/lib/x86/)
        set(tobii-dll "${tobii-libdir}/Tobii.EyeX.Client.dll")
        # we only care about the .lib for MSVC++ build anyway
        target_link_libraries(opentrack-tracker-tobii "${tobii-libdir}/Tobii.EyeX.Client.lib")
        # we only ever use the C headers due to Microsoft CRT ABI incompatibility with GNU
        target_include_directories(opentrack-tracker-tobii SYSTEM PUBLIC "${SDK_TOBII_EYEX}/include/eyex")

        install(FILES "${tobii-dll}" DESTINATION "${opentrack-hier-pfx}" PERMISSIONS ${opentrack-perms-exec})
        if(CMAKE_HOST_SYSTEM_NAME STREQUAL "Windows")
            file(TO_CMAKE_PATH "$ENV{SystemRoot}" sysroot)
            if (IS_DIRECTORY "${sysroot}/SysWOW64")
                set(src "${sysroot}/SysWOW64")
            else()
                set(src "${sysroot}/System32")
            endif()
            install(FILES "${src}/msvcp110.dll" DESTINATION ${opentrack-hier-pfx} PERMISSIONS ${opentrack-perms-exec})
            install(FILES "${src}/msvcr110.dll" DESTINATION ${opentrack-hier-pfx} PERMISSIONS ${opentrack-perms-exec})
        endif()
    endif()
endif()
